/*************************************************************************
;Project:   BLDC_LV24V_SPWM
;MCU:       dsPIC33EP128MC506
;Date:      2020.04.02
;File:      Math.s
;Function:  正弦波生成查表
***************************************************************************/
/*------------------------------------------------------------------------*/
    .include	"../SYSTEM/SinTab.inc"
    .section	.math_section,code
/*------------------------------------------------------------------------*/
;函数入口：输入的角度 w0
;函数出口：输入角度的正弦值、余弦值
;插值使用公式：value = Y0 + (remainder *（Y1-Y0）) >> 16
/*------------------------------------------------------------------------*/
_CalSinCos:
    mov		CORCON,w2	;将内核寄存器的值存入w2
    mov		DSRPAG,w3	;将读页寄存器的值存入w3
    push.d	w2		;将寄存器w2，w3压入堆栈
    mov		#TableSize,w1	
    mul.uu	w0,w1,w0	
    add		w1,w1,w1	
    movpag	#psvpage(_SinTable),DSRPAG  
    mov		#psvoffset(_SinTable),w2    
    add		w1,w2,w3	
    mov		[w3],w4		
    cp0		w0	
    bra		NZ,_jInterpolate   
    mov		w4,_tCal + sSinValue 
    /*------------------余弦值查找----------------------*/
    add.b	#0x40,w1    
    add		w1,w2,w1    
    mov		[w1],w0	    
    mov		w0,_tCal + sCosValue 
    pop.d	w2	    ;将寄存器w2，w3弹出堆栈
    mov		w2,CORCON   ;恢复内核寄存器的值
    mov		w3,DSRPAG   ;恢复读页寄存器的值
    return
    /*----------------在正弦表中找不到对应的值，则进行线性插值-------------------*/
    ;插值的方式：找到需要查找角度的前后两个角度对应的正余弦值，然后在中间插值
_jInterpolate:
    inc2.b	w1,w1	    
    add		w1,w2,w3    
    mov		[w3],w6	    
    subr	w4,w6,w5    
    mul.us	w0,w5,w6    
    add		w7,w4,w4    
    mov		w4,_tCal + sSinValue 
    /*------------------余弦值求取----------------------*/
    add.b	#0x3E,w1    
    add		w1,w2,w3    
    mov		[w3],w4	    
    inc2.b	w1,w1	    
    add		w1,w2,w3    
    mov		[w3],w6	    
    subr	w4,w6,w5    
    mul.us	w0,w5,w0    
    add		w1,w4,w0    
    mov		w0,_tCal + sCosValue 
    pop.d	w2	    ;将寄存器w2，w3弹出堆栈
    mov		w2,CORCON   ;恢复内核寄存器的值
    mov		w3,DSRPAG   ;恢复读页寄存器的值
    return
/*------------------------------------------------------------------------*/
    .section	.const,psv
/*------------------------------------------------------------------------*/
_SinTable:
    .word   0,	    1608,   3212,   4808,   6393,   7962,   9512,   11039
    .word   12539,  14010,  15446,  16846,  18204,  19519,  20787,  22005
    .word   23170,  24279,  25329,  26319,  27245,  28105,  28898,  29621
    .word   30273,  30852,  31356,  31785,  32137,  32412,  32609,  32728
    .word   32767,  32728,  32609,  32412,  32137,  31785,  31356,  30852
    .word   30273,  29621,  28898,  28105,  27245,  26319,  25329,  24279
    .word   23170,  22005,  20787,  19519,  18204,  16846,  15446,  14010
    .word   12539,  11039,  9512,   7962,   6393,   4808,   3212,   1608
    .word   0,	    -1608,  -3212,  -4808,  -6393,  -7962,  -9512,  -11039
    .word   -12539, -14010, -15446, -16846, -18204, -19519, -20787, -22005
    .word   -23170, -24279, -25329, -26319, -27245, -28105, -28898, -29621
    .word   -30273, -30852, -31356, -31785, -32137, -32412, -32609, -32728
    .word   -32767, -32728, -32609, -32412, -32137, -31785, -31356, -30852
    .word   -30273, -29621, -28898, -28105, -27245, -26319, -25329, -24279
    .word   -23170, -22005, -20787, -19519, -18204, -16846, -15446, -14010
    .word   -12539, -11039, -9512,  -7962,  -6393,  -4808,  -3212,  -1608
/*------------------------------------------------------------------------*/
    .end
/*------------------------------------------------------------------------*/
    